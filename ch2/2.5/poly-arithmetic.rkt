#!/usr/bin/env racket
#lang sicp

(#%require "dispatch.rkt")
(#%require "apply-generic.rkt")

(#%provide install-polynomial-package)
(#%provide make-polynomial)
(#%provide make-poly-sparse make-poly-dense)

(define (install-polynomial-package)
  ;; internal procedures
  ;; representation of poly
  (define (make-poly variable term-list) (cons variable term-list))
  (define (variable p) (car p))
  (define (term-list p) (cdr p))
  (define (same-variable? v1 v2)
    (and (variable? v1) (variable? v2) (eq? v1 v2)))
  (define (variable? x) (symbol? x))
  ;; representation of terms and lists
  (define (the-empty-termlist) '())
  (define (make-term order coeff) (list order coeff))
  (define (order term) (car term))
  (define (coeff term) (cadr term))
  (define (=zero-poly? poly)
    (cond ((empty-termlist? (term-list poly)) #t)
          ((not (=zero? (coeff (first-term (term-list poly))))) #f)
          (else (=zero-poly? (make-poly
                               (variable poly)
                               (rest-terms (term-list poly)))))))
  (define (add-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (make-poly (variable p1)
                   (add-terms (term-list p1) (term-list p2)))
        (error "Polys not in same var: ADD-POLY" (list p1 p2))))
  (define (add-terms L1 L2)
    (cond ((empty-termlist? L1) L2)
          ((empty-termlist? L2) L1)
          (else
            (let ((t1 (first-term L1))
                  (t2 (first-term L2)))
              (cond ((> (order t1) (order t2))
                     (adjoin-term
                       t1
                       (add-terms (rest-terms L1) L2)))
                    ((< (order t1) (order t2))
                     (adjoin-term
                       t2
                       (add-terms L1 (rest-terms L2))))
                    (else
                      (adjoin-term
                        (make-term (order t1)
                                   (add (coeff t1) (coeff t2)))
                        (add-terms (rest-terms L1)
                                   (rest-terms L2)))))))))
  (define (sub-poly p1 p2)
    ; if use generic operator, the polynomial tag has been
    ; stripped so it won't work
    (add-poly p1 (neg-poly p2)))
  (define (neg-poly poly)
    (define (negate-termlist tl)
      (if (empty-termlist? tl)
          ; need type for adjoin-term
          (attach-tag (type-tag (term-list poly))
                      (the-empty-termlist))
          (adjoin-term (make-term (order (first-term tl))
                                  (negate (coeff (first-term tl))))
                       (negate-termlist (rest-terms tl)))))
    (make-poly (variable poly) (negate-termlist (term-list poly))))
  (define (mul-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (make-poly (variable p1)
                   (mul-terms (term-list p1) (term-list p2)))
        (error "Polys not in same var: MUL-POLY" (list p1 p2))))
  (define (mul-terms L1 L2)
    (if (empty-termlist? L1)
        (attach-tag (type-tag L1) (the-empty-termlist))
        (add-terms (mul-term-by-all-terms (first-term L1) L2)
                   (mul-terms (rest-terms L1) L2))))
  (define (mul-term-by-all-terms t1 L)
    (if (empty-termlist? L)
        ; needs to be tagged for add-terms
        (attach-tag (type-tag L) (the-empty-termlist))
        (let ((t2 (first-term L)))
          (adjoin-term
            (make-term (+ (order t1) (order t2))
                       (mul (coeff t1) (coeff t2)))
            (mul-term-by-all-terms t1 (rest-terms L))))))
  (define (div-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (make-poly (variable p1)
                   (div-terms (term-list p1) (term-list p2)))
        (error "Polys not in same var: DIV-POLY" (list p1 p2))))
  (define (div-terms L1 L2)
    (if (empty-termlist? L1)
        (list (cons 'sparse (the-empty-termlist))
              (cons 'sparse (the-empty-termlist)))
        (let ((t1 (first-term L1))
              (t2 (first-term L2)))
          (if (> (order t2) (order t1))
              (list (cons (type-tag L1) (the-empty-termlist)) L1)
              (let ((new-c (div (coeff t1) (coeff t2)))
                    (new-o (- (order t1) (order t2))))
                (let ((rest-of-result
                        (div-terms
                          ; sub makes new poly, want just terms
                          (contents
                            (term-list
                              (sub
                                (make-polynomial 'x L1)
                                (make-polynomial
                                  'x
                                  (mul-terms
                                    (adjoin-term
                                      (make-term new-o new-c)
                                      (cons (type-tag L2) (the-empty-termlist)))
                                    L2)))))
                          L2)))
                  (list
                    (simplify
                      (adjoin-term (make-term new-o new-c)
                                   (car rest-of-result)))
                    (simplify (cadr rest-of-result)))))))))
  (define (gcd-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (gcd-terms (term-list p1) (term-list p2))
        (error "Polys not in same var: GCD-POLY" (list p1 p2))))
  (define (reduce-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (map
          (lambda (term-list)
            (make-polynomial (variable p1) term-list))
          (reduce-terms (term-list p1) (term-list p2)))
        (error "Polys not in same var: REDUCE-POLY" (list p1 p2))))
  (define (reduce-terms n d)
    (let ((nd-gcd (gcd-terms n d)))
      (let ((c (coeff (first-term nd-gcd)))
            (O1 (max (order (first-term n))
                     (order (first-term d))))
            (O2 (order (first-term nd-gcd))))
        (let ((integerizing-factor (expt c (+ 1 (- O1 O2)))))
          (let ((n2 (term-map n mul integerizing-factor))
                (d2 (term-map d mul integerizing-factor)))
            ; there will be no remainder
            (let ((n3 (car (div-terms n2 nd-gcd)))
                  (d3 (car (div-terms d2 nd-gcd))))
              (let ((nd-coeff-gcd
                      (apply gcd (append (get-coeff n3) (get-coeff d3)))))
                (let ((nn (term-map n3 div nd-coeff-gcd))
                      (dd (term-map d3 div nd-coeff-gcd)))
                  (list nn dd)))))))))
  (define (gcd-terms a b)
    (if (empty-termlist? b)
        (reduce-coeff a)
        (gcd-terms b (pseudoremainder-terms a b))))
  (define (remainder-terms a b)
    (cadr (div-terms a b)))
  (define (pseudoremainder-terms a b)
    (let ((c (coeff (first-term b)))
          (O1 (order (first-term a)))
          (O2 (order (first-term b))))
      (let ((integerizing-factor (expt c (+ 1 (- O1 O2)))))
        (cadr (div-terms (term-map a * integerizing-factor) b)))))
  (define (reduce-coeff term-list)
    (let ((coeff-gcd (apply gcd (get-coeff term-list))))
      (term-map term-list div coeff-gcd)))
  (define (get-coeff term-list)
    (accumulate
      (lambda (x y) (cons (coeff x) y))
      '()
      (contents term-list)))
  (define (accumulate op init seq)
    (if (null? seq)
        init
        (op (car seq)
            (accumulate op init (cdr seq)))))
  (define (term-map term-list op value)
    (attach-tag (type-tag term-list)
                (map
                  (lambda (term)
                    (make-term
                      (order term)
                      (op (coeff term) value)))
                  (contents term-list))))

  ;; interface to rest of the system
  (define (tag p) (attach-tag 'polynomial p))
  (put 'add '(polynomial polynomial)
       (lambda (p1 p2) (tag (add-poly p1 p2))))
  (put 'sub '(polynomial polynomial)
       (lambda (p1 p2) (tag (sub-poly p1 p2))))
  (put 'mul '(polynomial polynomial)
       (lambda (p1 p2) (tag (mul-poly p1 p2))))
  (put 'div '(polynomial polynomial)
       (lambda (p1 p2) (tag (div-poly p1 p2))))
  (put 'greatest-common-divisor '(polynomial polynomial) gcd-poly)
  (put 'negate '(polynomial)
       (lambda (p) (tag (neg-poly p))))
  (put '=zero? '(polynomial) =zero-poly?)
  (put 'reduce '(polynomial polynomial) reduce-poly)
  (put 'make 'polynomial
       (lambda (var terms) (tag (make-poly var terms))))
  'done)

(define (make-polynomial var terms)
  ((get 'make 'polynomial) var terms))

;; generic operators for term-list representations
(define (adjoin-term term term-list)
  ((get 'adjoin-term (type-tag term-list)) term (contents term-list)))
(define (first-term term-list)
  (apply-generic 'first-term term-list))
(define (rest-terms term-list)
  (apply-generic 'rest-terms term-list))
(define (empty-termlist? term-list)
  (apply-generic 'empty-termlist? term-list))

(define (install-sparse-representation)
  (define (coeff term) (cadr term))
  (define (adjoin-term term term-list)
      (if (=zero? (coeff term))
          term-list
          (cons term term-list)))
  (define (first-term term-list) (car term-list))
  (define (rest-terms term-list) (cdr term-list))
  (define (empty-termlist? term-list) (null? term-list))
  (define (tag x) (attach-tag 'sparse x))
  (put 'adjoin-term '(sparse)
       (lambda (x y) (tag (adjoin-term x y))))
  (put 'first-term '(sparse) first-term)
  (put 'rest-terms '(sparse)
       (lambda (x) (tag (rest-terms x))))
  (put 'empty-termlist? '(sparse) empty-termlist?)
  (put 'simplify '(sparse) (lambda (x) (tag x)))
  (put 'make 'sparse
       (lambda (x y) (make-polynomial x (tag y))))
  'done)

(define (make-poly-sparse var terms)
  ((get 'make 'sparse) var terms))

(define (install-dense-representation)
  (define (order term) (car term))
  (define (coeff term) (cadr term))
  (define (adjoin-term term term-list)
    (let ((curr-order (length term-list)))
      (cond ((= (order term) curr-order)
             (cons (coeff term) term-list))
            ((> (order term) curr-order)
             (adjoin-term term (cons 0 term-list)))
            ((< (order term) curr-order)
             (if (=zero? (car term-list))
                 (adjoin-term term (cdr term-list))
                 (cons (car term-list)
                       (adjoin-term term (cdr term-list))))))))
  (define (first-term term-list)
    ;; construct so that it is order coeff list form
    (define (iter term-list order-count)
      (cond ((null? term-list)
             (error "Empty term list" term-list))
            ((not (=zero? (car term-list)))
             (list order-count (car term-list)))
            (else (iter (cdr term-list) (- order-count 1)))))
    (iter term-list (- (length term-list) 1)))
  (define (rest-terms term-list)
    (cond ((null? term-list)
           (error "Empty term list" term-list))
          ((not (=zero? (car term-list)))
           (cdr term-list))
          (else (rest-terms (cdr term-list)))))
  (define (empty-termlist? term-list)
    (cond ((null? term-list) #t)
          ((all-zeros? term-list) #t)
          (else #f)))
  (define (all-zeros? term-list)
    (cond ((null? term-list) #t)
          ((=zero? (car term-list))
           (all-zeros? (cdr term-list)))
          (else #f)))
  (define (simplify term-list)
    (cond ((=zero? (car term-list))
           (simplify (cdr term-list)))
          (else term-list)))
  (define (tag x) (attach-tag 'dense x))
  (put 'adjoin-term '(dense)
       (lambda (x y) (tag (adjoin-term x y))))
  (put 'first-term '(dense) first-term)
  (put 'rest-terms '(dense)
       (lambda (x) (tag (rest-terms x))))
  (put 'empty-termlist? '(dense) empty-termlist?)
  (put 'simplify '(dense)
       (lambda (x) (tag (simplify x))))
  (put 'make 'dense
       (lambda (x y) (make-polynomial x (tag y))))
  'done)

(define (make-poly-dense var terms)
  ((get 'make 'dense) var terms))

(define (add x y) (apply-generic 'add x y))
(define (sub x y) (apply-generic 'sub x y))
(define (mul x y) (apply-generic 'mul x y))
(define (div x y) (apply-generic 'div x y))
(define (negate x) (apply-generic 'negate x))
(define (=zero? x) (apply-generic '=zero? x))
(define (simplify x) (apply-generic 'simplify x))

(install-sparse-representation)
(install-dense-representation)
