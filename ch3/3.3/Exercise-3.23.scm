#!/usr/bin/env racket
#lang sicp

(define (make-deque) (cons '() '()))

(define (front-ptr deque) (car deque))
(define (rear-ptr deque) (cdr deque))
(define (set-front-ptr! deque item)
  (set-car! deque item))
(define (set-rear-ptr! deque item)
  (set-cdr! deque item))

(define (data pair) (caar pair))
(define (fwd-ptr pair) (cdr pair))
(define (rev-ptr pair) (cdar pair))
(define (set-fwd-ptr! pair item)
  (set-cdr! pair item))
(define (set-rev-ptr! pair item)
  (set-cdr! (car pair) item))

(define (empty-deque? deque)
  (or (null? (front-ptr deque))
      (null? (rear-ptr deque))))
(define (front-deque deque)
  (if (empty-deque? deque)
      (error "FRONT called with an empty deque" deque)
      (data (front-ptr deque))))
(define (rear-deque deque)
  (if (empty-deque? deque)
      (error "FRONT called with an empty deque" deque)
      (data (rear-ptr deque))))

(define (front-insert-deque! deque item)
  (let ((new-pair (cons (cons item '()) '()))) 
    (cond ((empty-deque? deque)
           (set-front-ptr! deque new-pair)
           (set-rear-ptr! deque new-pair)
           deque)
          (else
            (set-rev-ptr! (front-ptr deque) new-pair)
            (set-fwd-ptr! new-pair (front-ptr deque))
            (set-front-ptr! deque new-pair)
            deque))))
(define (rear-insert-deque! deque item)
  (let ((new-pair (cons (cons item '()) '())))
    (cond ((empty-deque? deque)
           (set-front-ptr! deque new-pair)
           (set-rear-ptr! deque new-pair)
           deque)
          (else
            (set-fwd-ptr! (rear-ptr deque) new-pair)
            (set-rev-ptr! new-pair (rear-ptr deque))
            (set-rear-ptr! deque new-pair)
            deque))))

(define (front-delete-deque! deque)
  (cond ((empty-deque? deque)
         (error "FRONT-DELETE! called with an empty deque" deque))
        (else
          (cond ((null? (fwd-ptr (front-ptr deque)))
                 (set-front-ptr! deque '()))
                (else
                  (set-rev-ptr! (fwd-ptr (front-ptr deque)) '())
                  (set-front-ptr! deque (fwd-ptr (front-ptr deque)))))
          deque)))
(define (rear-delete-deque! deque)
  (cond ((empty-deque? deque)
         (error "REAR-DELETE! called with an empty deque" deque))
        (else
          (cond ((null? (rev-ptr (rear-ptr deque)))
                 (set-rear-ptr! deque '()))
                (else
                  (set-fwd-ptr! (rev-ptr (rear-ptr deque)) '())
                  (set-rear-ptr! deque (rev-ptr (rear-ptr deque)))))
          deque)))

(define (print-deque deque)
  (define (printer items)
    (if (null? items)
        '()
        (cons (data items) (printer (fwd-ptr items)))))
  (if (empty-deque? deque)
      '()
      (printer (front-ptr deque))))

(define d1 (make-deque))
(front-insert-deque! d1 'a)
(print-deque d1)
(rear-insert-deque! d1 'b)
(print-deque d1)
(front-delete-deque! d1)
(print-deque d1)
(rear-insert-deque! d1 'c)
(print-deque d1)
(front-insert-deque! d1 'a)
(print-deque d1)
(rear-delete-deque! d1)
(print-deque d1)
(front-delete-deque! d1)
(print-deque d1)
(rear-delete-deque! d1)
(print-deque d1)
(front-insert-deque! d1 'a)
(print-deque d1)
